module trans/desugar

imports
  src-gen/signatures/SL-sig
  libstratego-lib

signature
  sorts FunDefSeq ArgSeq

  constructors
    ProgramDesug : List(FunDef) * Expr -> Program

  constructors // built-ins
    SLDefineFunction : Expr -> Expr
    SLNanoTime : Expr
    SLNewObject : Expr
    SLPrintln : Expr -> Expr
    SLReadln : Expr
    SLStacktrace : Expr

rules

  desugar-editor:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path;
      result   := <desugar-all> ast
  
  desugar-all = innermost(desugar); alltd(unquote(?'"'))

  desugar:
    Program(funcs) -> ProgramDesug(funcs, FunCall("main", []))
  
  desugar:
    IfNoElse(cond, block) -> IfThenElse(cond, block, Block([]))
  
  desugar:
    FunCall("defineFunction", [e]) -> SLDefineFunction(e)
  
  desugar:
    FunCall("nanoTime", []) -> SLNanoTime()
  
  desugar:
    FunCall("new", []) -> SLNewObject()
  
  desugar:
    FunCall("println", [e]) -> SLPrintln(e)
  
  desugar:
    FunCall("readln", []) -> SLReadln()
  
  desugar:
    FunCall("stacktrace", []) -> SLStacktrace()

module trans/semantics/functions

imports
  trans/semantics/values

/*
* Function definitions
*/
signature
  arrows
    FunDefSeq -load-> U
    FunDef -load-> U
    FunDef -fun2val-> V
    
rules

  NoFunDefSeq() -load-> U().

  FunDefSeq(fundef, fundefs) -load-> U()
  where
    fundef -load-> _;
    fundefs -load-> _.

  FunDefDesug(name, params, body) :: C C(E), E E' -load-> U() :: C C({entry, E}), E {entry, E'}
  where
    {name |--> FunV(name, params, body)} => entry.

rules

  FunCallDesug(name, args) --> v
  where
    lookup(name) --> FunV(_, params, body);
    args --> vargs;
    invoke(vargs, params, body) --> v.

/*
* Function calls
*/
signature
  constructors
    invoke: VSeq * ParamSeq * Stmt --> V
    bind: VSeq * ParamSeq --> E
  
  sorts
    ReturnHandler
  
  constructors
    doReturn : V -> ReturnHandler // will throw a ReturnException with the value and the semantic components
    onReturn : Stmt -> ReturnHandler // will catch a ReturnException with the value and the semantic components
  
  
  
  arrows
    ReturnHandler --> V // natively implemented
  
  sorts VSeq
  
  constructors
    VSeq: V * VSeq -> VSeq
    NoVSeq: VSeq
  
  arrows
    ArgSeq --> VSeq

rules  

  invoke(vseq, parseq, stmt) :: E --> v :: E
  where
    bind(vseq, parseq) :: E --> E' :: E _;
    onReturn(stmt) :: E' --> v :: E _.
  
  NoArgSeq() --> NoVSeq().
  
  ArgSeq(e, aseq) --> VSeq(e, aseq).
  
  bind(vals, params) :: E, C --> Ecall :: E', C'
  where
    vals => NoVSeq()
    < {
	    params => NoParamSeq();
	    C => C(Etop);
	    Etop => Ecall;
	    E => E';
	    C => C'
	  } + {
	    vals => VSeq(av, avseq);
      params => ParamSeq(p, pseq);
      bind(avseq, pseq) :: E, C --> Ebind :: E', C';
      {p |--> av, Ebind} => Ecall
	  }.
  
  ReturnVoid() --> U()
  where
    doReturn(NullV()) --> _.

  Return(v) --> U()
  where
    doReturn(v) --> _.




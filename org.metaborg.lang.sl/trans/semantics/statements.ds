module trans/semantics/control-flow

imports
  trans/semantics/desugared-sig
  trans/semantics/values

// While loops
signature
  constructors
  whileHelper : Expr * List(Stmt) -> Stmt

rules
  
  While(e, Block(ss)) --> U()
   where
     onBreak(onContinue(whileHelper(e, ss))) --> _.
 
  w@whileHelper(BoolV(true), ss) --> U()
  where
    ss --> _;
    w --> _.
  
  whileHelper(BoolV(false), _) --> U().

signature
  sorts
    BreakHandler
    ContinueHandler
    
  constructors
    doBreak : BreakHandler // will throw a BreakException with the value and the semantic components 
    onBreak : Stmt -> Stmt // will handle a BreakException with the value and the semantic components
    doContinue : ContinueHandler // will throw a ContinueException with the value and the semantic components
    onContinue : Stmt -> Stmt // will handle a ContinueException with the value and the semantic components  

   arrows
     BreakHandler --> U // natively implemented
     ContinueHandler --> U // natively implemented

rules
  
  Continue() --> u
  where
    doContinue() --> u.
  
  Break() --> u
  where
    doBreak() --> u.

rules

  IfThenElse(BoolV(true), Block(s1), _) --> s1.
  
  IfThenElse(BoolV(false), _, Block(s2)) --> s2.
  
  Stmt(e) --> U()
  where
    e --> _.
  
  __Block2Stmt__(Block(ss)) --> ss.
  
signature
  arrows
    List(Stmt) --> U

rules

  [] : List(Stmt) --> U().
  
  [s | xs] : List(Stmt) --> U()
  where
    s --> _;
    xs --> _.
